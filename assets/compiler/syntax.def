// This file defines the Keid syntax in Keid Syntax Definition Language (KSDL).
// The parser for this file can be found in `parser.keid`.
// See "syntax.def.README.md" for documentation regarding the format of this file.

RULE Newline
    OR(LITERAL \r\n, LITERAL \n)
END RULE

RULE Identifier
    OR(RANGE A-Z, RANGE a-z, LITERAL _)
    OPTIONAL REPEATED OR(RANGE A-Z, RANGE a-z, LITERAL _, RANGE 0-9)
END RULE

RULE TypeModifier
    LITERAL public
END RULE

RULE ClassDecl
    $modifiers = OPTIONAL REPEATED RULE TypeModifier
    LITERAL class
    MOVE
    $name = RULE Identifier
END RULE

RULE Namespace
    RULE Identifier
    OPTIONAL REPEATED COMPOUND
        LITERAL ::
        RULE Identifier
    END COMPOUND
END RULE

RULE NamespaceStatement
    LITERAL namespace
    MOVE
    $ns = RULE Namespace
END RULE

RULE ImportStatement
    LITERAL import
    MOVE
    $ns = RULE Namespace
END RULE

RULE RootStatement
    MOVE
    $st = OR(RULE ImportStatement, RULE ClassDecl)
    RULE Newline
END RULE

RULE Program
    OPTIONAL REPEATED RULE Newline
    $ns = RULE NamespaceStatement
    OPTIONAL REPEATED RULE Newline
    $st = OPTIONAL REPEATED RULE RootStatement
    OPTIONAL REPEATED RULE Newline
END RULE
