namespace keidc::ksdl

import (
    core::collections
    core::object
    core::string
)

public enum StatementResult {
    /// Returned by statements when they match with a token result.
    Match {
        matches: List<ParserMatch>
    }
    /// Returned by statements which don't perform matching operations, e.g. `move`.
    /// A matchless result is always considered successful.
    Matchless
    /// Returned by statements when they attempt to match a token and fail.
    Failure
    /// Returned by statements when a hard fail occurs.
    ImmediateFailure {
        statement: ParserStatement
        result: Box<StatementResult>
        loc: TokenLocation
    }
}

public interface ParserStatement {
    /// Returns true if the rule matched, otherwise false.
    execute(stream: TokenStream, state: RuleState): StatementResult

    describe(): string
}

public class MoveStatement { }

implement ParserStatement for MoveStatement {
    execute(_: TokenStream, state: RuleState): StatementResult {
        state.moved = true

        return StatementResult.Matchless
    }

    describe(): string => ""
}

public class EOFStatement { }

implement ParserStatement for EOFStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            EOF => {
                stream.next()
                return new StatementResult.Match {
                    matches = List.from<ParserMatch>(new ParserMatch.Token {
                        token
                    })
                }
            }
            _ => return StatementResult.Failure
        }
    }

    describe(): string => "EOF"
}

public class TokenStatement { }

implement ParserStatement for TokenStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            EOF => return StatementResult.Failure
            _ => {
                stream.next()
                return new StatementResult.Match {
                    matches = List.from<ParserMatch>(new ParserMatch.Token {
                        token
                    })
                }
            }
        }
    }

    describe(): string => "any token"
}

public class OperatorStatement {
    op: string

    public static create(op: string): OperatorStatement => new OperatorStatement { op, }
}

implement ParserStatement for OperatorStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            Operator { op, } => {
                if op == this.op {
                    stream.next()
                    return new StatementResult.Match {
                        matches = List.from<ParserMatch>(new ParserMatch.Token {
                            token
                        })
                    }
                }
            }
            _ => {}
        }
        return StatementResult.Failure
    }

    describe(): string => string.format("operator '", this.op, "'")
}

public class KeywordStatement {
    keyword: string

    public static create(keyword: string): KeywordStatement => new KeywordStatement { keyword, }
}

implement ParserStatement for KeywordStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            Keyword { name, } => {
                if name == this.keyword {
                    stream.next()
                    return new StatementResult.Match {
                        matches = List.from<ParserMatch>(new ParserMatch.Token {
                            token
                        })
                    }
                }
            }
            _ => {}
        }
        return StatementResult.Failure
    }

    describe(): string => string.format("keyword '", this.keyword, "'")   
}

public class LiteralStatement { }

implement ParserStatement for LiteralStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            Literal { lit, } => {
                stream.next()
                return new StatementResult.Match {
                    matches = List.from<ParserMatch>(new ParserMatch.Token {
                        token
                    })
                }
            }
            _ => {}
        }
        return StatementResult.Failure
    }

    describe(): string => "literal"
}

public class NewlineStatement { }

implement ParserStatement for NewlineStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        match stream.currentToken.kind {
            Newline => {
                stream.next()
                return StatementResult.Matchless
            }
            _ => {}
        }
        return StatementResult.Failure
    }

    describe(): string => "newline"
}

public class IdentifierStatement {
    prefix: char
}

implement ParserStatement for IdentifierStatement {
    execute(stream: TokenStream, _: RuleState): StatementResult {
        let token = stream.currentToken
        match token.kind {
            Identifier { name, } => {
                if this.prefix != '\0' {
                    if this.prefix != name.chars[0] {
                        return StatementResult.Failure
                    }
                }
                stream.next()
                return new StatementResult.Match {
                    matches = List.from<ParserMatch>(new ParserMatch.Token {
                        token
                    })
                }
            }
            _ => {}
        }
        return StatementResult.Failure
    }

    describe(): string => "identifier"
}

public class SubruleStatement {
    subrule: Rule

    public static create(subrule: Rule): SubruleStatement => new SubruleStatement { subrule, }
}

implement ParserStatement for SubruleStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let ruleResult = this.subrule.parse(stream)
        return match ruleResult {
            Success { match, } => new StatementResult.Match {
                matches = List.from<ParserMatch>(match)
            }
            SoftFail => StatementResult.Failure
            HardFail { statement, result, loc, } => new StatementResult.ImmediateFailure { statement, result, loc, }
        }
    }

    describe(): string => this.subrule.name
}

public class UntilStatement {
    statement: ParserStatement
    inclusive: bool

    public static create(statement: ParserStatement, inclusive: bool): UntilStatement => new UntilStatement { statement, inclusive, }
}

implement ParserStatement for UntilStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let tokens = List.empty<ParserMatch>()
        loop {
            match this.statement.execute(stream, state) {
                Match { matches, } => {
                    if this.inclusive {
                        tokens.extend(matches)
                    }
                    break
                }
                Matchless => break
                _ => {
                    tokens.push(new ParserMatch.Token {
                        token = stream.currentToken
                    })
                    stream.next()
                }
            }
        }
        return new StatementResult.Match {
            matches = tokens
        }
    }

    describe(): string => string.format("all tokens until ", this.statement.describe())
}

public class OptionalStatement {
    statement: ParserStatement

    public static create(statement: ParserStatement): OptionalStatement => new OptionalStatement { statement, }
}

implement ParserStatement for OptionalStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let offset = stream.offset
        match this.statement.execute(stream, state) {
            Match { matches, } => return new StatementResult.Match { matches, }
            ImmediateFailure { statement, result, loc, } => return new StatementResult.ImmediateFailure { statement, result, loc, }
            Matchless => return StatementResult.Matchless
            Failure => {
                stream.offset = offset
                return StatementResult.Matchless
            }
        }
        unreachable
    }

    describe(): string => string.format("optional ", this.statement.describe())
}

public class OrStatement {
    options: List<ParserStatement>

    public static create(options: List<ParserStatement>): OrStatement => new OrStatement { options, }
}

implement ParserStatement for OrStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let offset = stream.offset
        for option in this.options {
            let result = option.execute(stream, state)
            match result {
                Failure => {
                    stream.offset = offset
                }
                _ => return result
            }
        }
        return StatementResult.Failure
    }

    describe(): string {
        if this.options.length == 2 {
            return string.format("either ", this.options.elementAt(0).describe(), " or ", this.options.elementAt(1).describe())
        }

        let sb = StringBuilder.empty()
        sb.append("either ")
        for i in range.create(this.options.length - 1) {
            sb.append(this.options.elementAt(i).describe())
            sb.append(", ")
        }
        sb.append("or ")
        sb.append(this.options.elementAt(this.options.length - 1).describe())
        return sb.toString()
    }
}

public class NotStatement {
    statement: ParserStatement

    public static create(statement: ParserStatement): NotStatement => new NotStatement { statement, }
}

implement ParserStatement for NotStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        return match this.statement.execute(stream, state) {
            Match { matches, } => StatementResult.Failure
            Matchless => StatementResult.Failure
            ImmediateFailure { statement, result, loc, } => new StatementResult.ImmediateFailure { statement, result, loc, }
            Failure => StatementResult.Matchless
        }
    }

    describe(): string => string.format("not ", this.statement.describe())
}

public class GroupStatement {
    groupName: string
    value: ParserStatement

    public static create(groupName: string, value: ParserStatement): GroupStatement => new GroupStatement { groupName, value, }
}

implement ParserStatement for GroupStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let result = this.value.execute(stream, state)
        match result {
            Match { matches, } => {
                for group in state.groups {
                    if group.variable == this.groupName {
                        group.values.extend(matches)
                        return result
                    }
                }
                state.groups.push(new RuleMatchGroup {
                    variable = this.groupName
                    values = matches
                })
            }
            _ => {}
        }

        return result
    }

    describe(): string => this.value.describe()
}

public class RepeatedStatement {
    child: ParserStatement
    optional: bool

    public static create(child: ParserStatement, optional: bool): RepeatedStatement => new RepeatedStatement { child, optional, }
}

implement ParserStatement for RepeatedStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        let resultMatches = List.empty<ParserMatch>()
        if !this.optional {
            match this.child.execute(stream, state) {
                Failure => return StatementResult.Failure
                ImmediateFailure { statement, result, loc, } => return new StatementResult.ImmediateFailure { statement, result, loc, }
                Match { matches, } => {
                    resultMatches.extend(matches)
                }
                _ => {}
            }
        }

        loop {
            let offset = stream.offset
            match this.child.execute(stream, state) {
                Failure => {
                    stream.offset = offset
                    break
                }
                ImmediateFailure { statement, result, loc, } => return new StatementResult.ImmediateFailure { statement, result, loc, }
                Match { matches, } => {
                    resultMatches.extend(matches)
                }
                _ => {}
            }
        }

        return new StatementResult.Match {
            matches = resultMatches
        }
    }

    describe(): string => this.child.describe()
}

public class CompoundStatement {
    statements: List<ParserStatement>

    public static create(statements: List<ParserStatement>): CompoundStatement => new CompoundStatement { statements, }
}

implement ParserStatement for CompoundStatement {
    execute(stream: TokenStream, state: RuleState): StatementResult {
        for statement in this.statements {
            match statement.execute(stream, state) {
                ImmediateFailure { statement, result, loc, } => return new StatementResult.ImmediateFailure { statement, result, loc, }
                Failure => return StatementResult.Failure
                _ => {}
            }
        }
        return StatementResult.Matchless
    }

    describe(): string {
        if this.statements.length == 2 {
            return string.format(this.statements.elementAt(0).describe(), " and ", this.statements.elementAt(1).describe())
        }

        let sb = StringBuilder.empty()
        for i in range.create(this.statements.length - 1) {
            sb.append(this.statements.elementAt(i).describe())
            sb.append(", ")
        }
        sb.append("and ")
        sb.append(this.statements.elementAt(this.statements.length - 1).describe())
        return sb.toString()
    }
}
