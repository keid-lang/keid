namespace keidc::ksdl

import (
    core::collections
    core::error
    core::object
    core::string
)

public enum LexerLiteral {
    Bool {
        val: bool
    }
    SignedInteger {
        val: int64
    }
    UnsignedInteger {
        val: uint64
    }
    String {
        val: String
    }
    Char {
        val: char
    }
    Null
}

implement ToString for LexerLiteral {
    toString(): string {
        return match this {
            Bool { val, } => string.format("Bool(", val.toString(), ")")
            SignedInteger { val, } => string.format("SignedInteger(", val.toString(), ")")
            UnsignedInteger { val, } => string.format("UnsignedInteger(", val.toString(), ")")
            String { val, } => string.format("String(", val, ")")
            Char { val, } => string.format("Char(", val.toString(), ")")
            Null => "Null"
        }
    }
}

public enum LexerTokenKind {
    Identifier {
        name: String
    }
    Keyword {
        name: String
    }
    Operator {
        op: String
    }
    Literal {
        lit: LexerLiteral
    }
    Newline
    EOF

    public identifier(): string {
        match this {
            Identifier { name, } => return name
            _ => throw Error.create("LexerTokenKind is not Identifier")
        }
    }

    public keyword(): string {
        match this {
            Keyword { name, } => return name
            _ => throw Error.create("LexerTokenKind is not Keyword")
        }
    }

    public operator(): string {
        match this {
            Operator { op, } => return op
            _ => throw Error.create("LexerTokenKind is not Operator")
        }
    }
}

implement ToString for LexerTokenKind {
    toString(): string {
        return match this {
            Identifier { name, } => string.format("Identifier(", name, ")")
            Keyword { name, } => string.format("Keyword(", name, ")")
            Operator { op, } => string.format("Operator(", op, ")")
            Literal { lit, } => string.format("Literal(", lit.toString(), ")")
            Newline => "Newline"
            EOF => "EOF"
        }
    }
}

public struct LexerToken {
    loc: TokenLocation
    kind: LexerTokenKind

    public get name: string => match this.kind {
        Identifier => "identifier"
        Keyword => "keyword"
        Operator => "operator"
        Literal => "literal"
        Newline => "newline"
        EOF => "EOF"
    }
}

implement ToString for LexerToken {
    toString(): string {
        return string.format("LexerToken(", this.kind.toString(), " @ ", this.loc.toString(), ")")
    }
}

enum StringTokenizerState {
    Normal
    Escape
}

const KEYWORDS: [string] = new string[
    "as", "class", "const", "constructor", "deref", "destructor", "else", "end macro",
    "enum", "extends", "extern", "for", "function", "get", "if", "implement", "import",
    "in", "interface", "let", "macro", "match", "namespace", "public", "ref", "return",
    "set", "static", "struct", "throw", "type", "unsafe"
]
const OPERATORS: [string] = new string[
    "==", "!=", "+=", "-=", "*=", "/=", "%=", "<<=", ">>=", "&=", "|=", "^=",
    "+", "-", "*", "/", "%", "<<", ">>", "<=", ">=", "=>", "<", ">", "!",
    "...", "??", "?", "&", "|", "^", "=", ",", "@", "::", ":", ".", "(",
    ")", "[", "]", "{", "}", ";", "`",
]
const KEYWORD_OPERATORS: [string] = new string["and", "or", "not", "as"]

public function tokenize(reader: TokenReader): List<LexerToken> {
    let tokens = List.empty<LexerToken>()
    let currentToken = StringBuilder.empty()

    reader.isSkipWhitespace = true
    while !reader.isFinished {
        reader.skipWhitespace()
        if reader.currentCharacter == '\n' || reader.startsWith("\r\n") {
            tokens.push(new LexerToken {
                loc = TokenLocation.create(reader.cursor, 1)
                kind = LexerTokenKind.Newline
            })
            let adv: usize = match reader.currentCharacter {
                '\n' => 1
                _ => 2
            }
            reader.advance(adv)
        } else if reader.currentCharacter == '"' || reader.currentCharacter == '\'' {
            let start = reader.cursor
            let delimiter = reader.currentCharacter
            reader.advance(1)
            reader.isSkipWhitespace = false
            let str = tokenizeString(reader, delimiter)
            reader.isSkipWhitespace = true
            if delimiter == '\'' && str.length != 1 {
                throw Error.create("character literal must be exactly one character")
            }
            let lit = match delimiter {
                '"' => new LexerLiteral.String {
                    val = str
                }
                '\'' => new LexerLiteral.Char {
                    val = str.chars[0]
                }
            }
            tokens.push(new LexerToken {
                loc = TokenLocation.create(start, reader.cursor - start)
                kind = new LexerTokenKind.Literal {
                    lit
                }
            })
        } else if reader.currentCharacter.isNumber || reader.currentCharacter == '+' || reader.currentCharacter == '-' {
            let c = reader.currentCharacter
            let cursor = reader.cursor
            reader.advance(1)
            if (c == '+' || c == '-') && !reader.currentCharacter.isNumber {
                tokens.push(new LexerToken {
                    loc = TokenLocation.create(cursor, 1)
                    kind = new LexerTokenKind.Operator {
                        op = c.toString()
                    }
                })
            } else {
                let sb = StringBuilder.withCapacity(1)
                sb.append(c)

                reader.isSkipWhitespace = false
                while reader.currentCharacter.isNumber {
                    c = reader.currentCharacter
                    reader.advance(1)
                    sb.append(c)
                }
                reader.isSkipWhitespace = true

                let s = sb.toString()
                let lit = match s.startsWith("-") {
                    true => new LexerLiteral.SignedInteger {
                        val = Int64.parse(s)
                    }
                    false => new LexerLiteral.UnsignedInteger {
                        val = UInt64.parse(s)
                    }
                }

                tokens.push(new LexerToken {
                    loc = TokenLocation.create(cursor, reader.cursor - cursor)
                    kind = new LexerTokenKind.Literal {
                        lit
                    }
                })
            }
        } else {
            if advanceComment(reader) {
                continue
            }
            let cursor = reader.cursor
            if advanceKeyword(reader, "null") {
                tokens.push(new LexerToken {
                    loc = TokenLocation.create(cursor, 4)
                    kind = new LexerTokenKind.Literal {
                        lit = LexerLiteral.Null
                    }
                })
                continue
            }
            if advanceKeyword(reader, "true") {
                tokens.push(new LexerToken {
                    loc = TokenLocation.create(cursor, 4)
                    kind = new LexerTokenKind.Literal {
                        lit = new LexerLiteral.Bool {
                            val = true
                        }
                    }
                })
                continue
            }
            if advanceKeyword(reader, "false") {
                tokens.push(new LexerToken {
                    loc = TokenLocation.create(cursor, 5)
                    kind = new LexerTokenKind.Literal {
                        lit = new LexerLiteral.Bool {
                            val = false
                        }
                    }
                })
                continue
            }
            if tokenizeOperator(reader, tokens) {
                continue
            }
            if tokenizeKeywordOperator(reader, tokens) {
                continue
            }
            if tokenizeKeyword(reader, tokens) {
                continue
            }
            if tokenizeIdentifier(reader, tokens) {
                continue
            }

            let pos = reader.documentPosition
            throw Error.create(string.format("unexpected token '", reader.currentCharacter.toString(), "' at ", pos.line.toString(), ":", pos.col.toString()))
        }
    }

    return tokens
}

function advanceComment(reader: TokenReader): bool {
    if reader.startsWith("//") {
        reader.advance(2)
        while reader.currentCharacter != '\n' {
            reader.advance(1)
        }
        return true
    }
    return false
}

function advanceKeyword(reader: TokenReader, keyword: string): bool {
    if reader.startsWith(keyword) {
        reader.advance(keyword.length)
        if !isCharIdentifierComponent(reader.currentCharacter) {
            return true
        } else {
            reader.retreat(keyword.length)
        }
    }
    return false
}

function tokenizeKeyword(reader: TokenReader, tokens: List<LexerToken>): bool {
    for keyword in KEYWORDS {
        let cursor = reader.cursor
        if advanceKeyword(reader, keyword) {
            tokens.push(new LexerToken {
                loc = TokenLocation.create(cursor, keyword.length)
                kind = new LexerTokenKind.Keyword {
                    name = keyword
                }
            })
            return true
        }
    }
    return false
}

function tokenizeIdentifier(reader: TokenReader, tokens: List<LexerToken>): bool {
    if isCharIdentifierComponent(reader.currentCharacter) {
        let cursor = reader.cursor
        let sb = StringBuilder.withCapacity(1)
        while !reader.isFinished && isCharIdentifierComponent(reader.currentCharacter) {
            sb.append(reader.currentCharacter)
            reader.advance(1)
        }
        let ident = sb.toString()
        tokens.push(new LexerToken {
            loc = TokenLocation.create(cursor, ident.length)
            kind = new LexerTokenKind.Identifier {
                name = ident
            }
        })
        return true
    }
    return false
}

function tokenizeOperator(reader: TokenReader, tokens: List<LexerToken>): bool {
    for operator in OPERATORS {
        if reader.startsWith(operator) {
            tokens.push(new LexerToken {
                loc = TokenLocation.create(reader.cursor, operator.length)
                kind = new LexerTokenKind.Operator {
                    op = operator
                }
            })
            reader.advance(operator.length)
            return true
        }
    }
    return false
}

function tokenizeKeywordOperator(reader: TokenReader, tokens: List<LexerToken>): bool {
    for op in KEYWORD_OPERATORS {
        let cursor = reader.cursor
        if advanceKeyword(reader, op) {
            tokens.push(new LexerToken {
                loc = TokenLocation.create(cursor, op.length)
                kind = new LexerTokenKind.Operator {
                    op
                }
            })
            return true
        }
    }
    return false
}

function tokenizeString(reader: TokenReader, delimiter: char): string {
    let state = StringTokenizerState.Normal
    let builder = StringBuilder.empty()
    while !reader.isFinished {
        let c = reader.currentCharacter
        reader.advance(1)

        match state {
            Normal => {
                if c == delimiter {
                    return builder.toString()
                } else if c == '\\' {
                    state = StringTokenizerState.Escape
                } else {
                    builder.append(c)
                }
            }
            Escape => {
                let m = match c {
                    '"' => '"'
                    '\'' => '\''
                    '\\' => '\\'
                    '0' => '\0'
                    'n' => '\n'
                    'r' => '\r'
                    't' => '\t'
                    _ => throw Error.create("Invalid escape sequence")
                }
                builder.append(m)
                state = StringTokenizerState.Normal
            }
        }
    }
    throw Error.create("unexpected EOF while tokenizing string")
}

function isCharIdentifierComponent(c: char): bool {
    return (c >= 'A' && c <= 'Z') || (c >= 'a' && c <= 'z') || (c >= '0' && c <= '9') || c == '_' || c == '$' || c == '#'
}
