namespace keidc::ksdl

import (
    core::collections
    core::error
    core::object
    core::string
)

public enum ParserMatch {
    Rule {
        rule: RuleMatch
    }
    Token {
        token: LexerToken
    }

    public rule(): RuleMatch {
        match this {
            Rule { rule, } => return rule
            Token { token, } => throw Error.create(string.format("illegal call to rule() on Token variant of ParserMatch (", token.toString(), ")"))
        }
        unreachable
    }

    public token(): LexerToken {
        match this {
            Token { token, } => return token
            _ => throw Error.create("illegal call to token() on Rule variant of ParserMatch")
        }
    }
}

implement ToString for ParserMatch {
    toString(): string {
        return match this {
            Rule { rule, } => string.format("Rule(", rule.toString(), ")")
            Token { token, } => string.format("Token(", token.toString(), ")")
        }
    }
}

public class RuleState {
    moved: bool
    groups: List<RuleMatchGroup>
}

public enum RuleResult {
    Success {
        match: ParserMatch
    }
    SoftFail
    HardFail {
        statement: ParserStatement
        result: StatementResult
        loc: TokenLocation
    }
}

public class Rule {
    name: string
    statements: List<ParserStatement>

    public parse(stream: TokenStream): RuleResult {
        let state = new RuleState {
            moved = false
            groups = List.empty<RuleMatchGroup>()
        }

        let start = stream.currentToken.originalLoc.start
        for statement in this.statements {
            stream.currentRule = this     
            
            let result = statement.execute(stream, state)
            match result {
                Failure => {
                    if state.moved {
                        return new RuleResult.HardFail {
                            statement
                            result
                            loc = stream.currentToken.loc
                        }
                    }
                    return RuleResult.SoftFail
                }
                ImmediateFailure { statement, result, loc, } => return new RuleResult.HardFail { statement, result, loc, }
                _ => {}
            }
        }

        return new RuleResult.Success {
            match = new ParserMatch.Rule {
                rule = new RuleMatch {
                    ruleName = this.name
                    loc = new TokenLocation {
                        start
                        length = stream.currentToken.originalLoc.start - start
                    }
                    groups = state.groups
                }
            }
        }
    }
}
