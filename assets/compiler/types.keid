namespace keidc::types

import (
    core::collections
    core::error
    core::object
    core::string
    keidc::abi
    keidc::ast
    llvm
)

public class QualifiedIdent {
    name: string
    generics: List<ComplexType>

    public static withName(name: string): QualifiedIdent => new QualifiedIdent {
        name
        generics = List.empty<ComplexType>()
    }

    public static withGenerics(name: string, generics: List<ComplexType>): QualifiedIdent => new QualifiedIdent { name, generics, }
}

implement ToString for QualifiedIdent {
    toString(): string {
        let sb = StringBuilder.withCapacity(this.name.length)
        sb.append(this.name)
        if this.generics.length > 0 {
            sb.append("<")
            for generic in this.generics {
                sb.append(generic.toString())
                sb.append(", ")
            }
            sb.move(-2)
            sb.append(">")
        }
        return sb.toString()
    }
}

implement ToString for ComplexType {
    toString(): string {
        return match this {
            Basic { basic, } => basic.toString()
            Array { element, length, } => string.format("[", element.toString(), "; ", length.toString(), "]")
            Slice { element, } => string.format("[", element.toString(), "]")
            Nullable { element, } => string.format("?", element.toString())
            _ => "unknown"
        }
    }
}

public enum ComplexType {
    Basic {
        basic: BasicType
    }
    Array {
        element: Box<ComplexType>
        length: uint32
    }
    Slice {
        element: Box<ComplexType>
    }
    Nullable {
        element: Box<ComplexType>
    }
    Compound {
        types: List<ComplexType>
    }
    RawPointer {
        element: Box<ComplexType>
    }
}

// this should not be an interface, only here due to restrictions of the pre-0.1 compiler
// TODO: move to enum body in 0.1
public interface ComplexTypeImpl {
    get root: BasicType

    withRoot(root: ComplexType): ComplexType
}

implement ComplexTypeImpl for ComplexType {
    get root: BasicType {
        return match this {
            Basic { basic, } => basic
            Array { element, length, } => element.root
            Slice { element, } => element.root
            Nullable { element, } => element.root
            RawPointer { element, } => element.root
        }
    }

    withRoot(root: ComplexType): ComplexType {
        return match this {
            Basic { basic, } => root
            Array { element, length, } => new ComplexType.Array {
                element = element.withRoot(root)
                length
            }
            Slice { element, } => new ComplexType.Slice {
                element = element.withRoot(root)
            }
            Nullable { element, } => new ComplexType.Nullable {
                element = element.withRoot(root)
            }
            RawPointer { element, } => new ComplexType.RawPointer {
                element = element.withRoot(root)
            }
        }
    }
}

public enum BasicType {
    Void
    Bool
    Char
    UInt8
    UInt16
    UInt32
    UInt64
    Int8
    Int16
    Int32
    Int64
    Float32
    Float64
    USize
    ISize
    Ident { ident: QualifiedIdent }
    Null
    Unknown
    Never
}

implement ToString for BasicType {
    toString(): string {
        return match this {
            Void => "void"
            Bool => "bool"
            Char => "char"
            UInt8 => "uint8"
            UInt16 => "uint16"
            UInt32 => "uint32"
            UInt64 => "uint64"
            Int8 => "int8"
            Int16 => "int16"
            Int32 => "int32"
            Int64 => "int64"
            Float32 => "float32"
            Float64 => "float64"
            USize => "usize"
            ISize => "isize"
            Ident { ident } => ident.toString()
            Null => "null"
            Unknown => "unknown"
            Never => "never"
        }
    }
}

public interface ToLLVMType {
    get llvmType: LLVMType
}

implement ToLLVMType for BasicType {
    get llvmType: LLVMType => match this {
        Void => llvm.context.voidType
        Bool => llvm.context.int1Type
        Char => llvm.context.int32Type
        UInt8 => llvm.context.int8Type
        UInt16 => llvm.context.int16Type
        UInt32 => llvm.context.int32Type
        UInt64 => llvm.context.int64Type
        Int8 => llvm.context.int8Type
        Int16 => llvm.context.int16Type
        Int32 => llvm.context.int32Type
        Int64 => llvm.context.int64Type
        Float32 => llvm.context.floatType
        Float64 => llvm.context.doubleType
        USize => llvm.context.intPtrType
        ISize => llvm.context.intPtrType
        Ident { ident: QualifiedIdent } => unreachable
        Null => llvm.context.voidType
        Unknown => llvm.context.voidType
        Never => llvm.context.voidType
    }
}

implement ToLLVMType for ComplexType {
    get llvmType: LLVMType => match this {
        Basic { basic, } => basic.llvmType
        Array { element, length, } => AbiArray.create(element, length).llvmType
        Slice { element, } => AbiSlice.create(element).llvmType
        Nullable { element, } => AbiNullable.create(element).llvmType
        Compound { types, } => llvm.context.getPointerType(AbiClassMetadata.create().llvmType)
        RawPointer { element, } => llvm.context.getPointerType(element.llvmType)
    }
}

implement ToLLVMType for AstFunction {
    get llvmType: LLVMType {
        let paramTypes = List.withCapacity<LLVMType>(this.params.length)
        for param in this.params {
            paramTypes.push(param.type.node.llvmType)
        }
        let returnType: LLVMType
        if this.returnType == null {
            returnType = llvm.context.voidType
        } else {
            returnType = this.returnType!.node.llvmType
        }
        let varargs = match this.varargs {
            Native => true
            _ => false
        }
        return llvm.context.getFunctionType(returnType, paramTypes, varargs)
    }
}
