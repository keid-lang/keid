// This file defines the Keid syntax in Keid Syntax Definition Language (KSDL).
// The parser for this file can be found in `parser.keid`.
// See "syntax.def.README.md" for documentation regarding the format of this file.

ALIAS NewlineOrComma OR(REPEATED LITERAL NEWLINE, LITERAL COMMA)

RULE HexInteger CONTIGUOUS
    LITERAL 0x
    MOVE
    REPEATED OR(RANGE 0-9, RANGE a-f, RANGE A-F)
END RULE

RULE DecimalInteger CONTIGUOUS
    COMPOUND(RANGE 1-9, OPTIONAL REPEATED RANGE 0-9)
END RULE

RULE ZeroInteger CONTIGUOUS
    LITERAL 0
END RULE

RULE Integer CONTIGUOUS
    $sign = OPTIONAL OR(LITERAL +, LITERAL -)
    $value = OR(RULE HexInteger, RULE DecimalInteger, RULE ZeroInteger)
END RULE

RULE String
    LITERAL "
    UNTIL LITERAL "
END RULE

RULE Char
    LITERAL '
    UNTIL LITERAL '
END RULE

RULE Bool
    OR(LITERAL true, LITERAL false)
END RULE

RULE Null
    LITERAL null
END RULE

RULE Identifier CONTIGUOUS
    OR(RANGE A-Z, RANGE a-z, LITERAL _)
    OPTIONAL REPEATED OR(RANGE A-Z, RANGE a-z, LITERAL _, RANGE 0-9)
END RULE

RULE ArrayType
    LITERAL [
    $elementType = ALIAS ValueType
    LITERAL ]
END RULE

RULE NullableType
    LITERAL ?
    $elementType = ALIAS ValueType
END RULE

RULE BasicType
    $path = RULE QualifiedPath
    $generics = OPTIONAL RULE GenericArgs
END RULE

ALIAS ValueType OR(RULE ArrayType, RULE NullableType, RULE BasicType)

RULE Modifier
    OR(LITERAL public, LITERAL static, LITERAL extern, LITERAL unsafe)
END RULE

RULE GenericDecl
    $name = RULE Identifier
    OPTIONAL REPEATED COMPOUND(LITERAL :, $constraints = ALIAS ValueType)
END RULE

RULE GenericDecls
    LITERAL <
    $decls = RULE GenericDecl
    OPTIONAL REPEATED COMPOUND(LITERAL COMMA, $decls = RULE GenericDecl)
    LITERAL >
END RULE

RULE GenericArgs
    LITERAL <
    $args = ALIAS ValueType
    OPTIONAL REPEATED COMPOUND(LITERAL COMMA, $args = ALIAS ValueType)   
    LITERAL >
END RULE

RULE AttributeArgs
    LITERAL (
    OPTIONAL REPEATED COMPOUND($args = ALIAS ConstExpr, LITERAL COMMA)
    $args = OPTIONAL ALIAS ConstExpr
    LITERAL )
END RULE

RULE AttributeStatement
    LITERAL @
    $name = RULE QualifiedPath
    $generics = OPTIONAL RULE GenericArgs
    $args = OPTIONAL RULE AttributeArgs
END RULE

ALIAS Attributes OPTIONAL REPEATED COMPOUND($attributes = RULE AttributeStatement, LITERAL NEWLINE)

RULE FuncParamDecl
    $name = RULE Identifier
    LITERAL :
    $type = ALIAS ValueType
END RULE

RULE FuncVarargsDecl
    LITERAL ...
    $param = RULE FuncParamDecl
END RULE

RULE FuncNativeVarargsDecl
    LITERAL ...
END RULE

RULE FuncParamsDecl
    LITERAL (
    MOVE
    OPTIONAL REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($params = RULE FuncParamDecl, LITERAL COMMA, OPTIONAL REPEATED LITERAL NEWLINE)
    OPTIONAL OR($varargs = RULE FuncVarargsDecl, $params = RULE FuncParamDecl, $nativeVarargs = RULE FuncNativeVarargsDecl)
    OPTIONAL COMPOUND (LITERAL COMMA, REPEATED LITERAL NEWLINE)
    LITERAL )
END RULE

RULE MethodDecl
    ALIAS Attributes
    $modifiers = OPTIONAL REPEATED RULE Modifier
    $name = RULE Identifier
    $generics = OPTIONAL RULE GenericDecls
    $params = RULE FuncParamsDecl
    MOVE
    OPTIONAL COMPOUND(LITERAL :, $returnType = ALIAS ValueType)
    $body = OPTIONAL OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

RULE FunctionDecl
    ALIAS Attributes
    $modifiers = OPTIONAL REPEATED RULE Modifier
    LITERAL function
    MOVE
    $name = RULE Identifier
    $generics = OPTIONAL RULE GenericDecls
    $params = RULE FuncParamsDecl
    MOVE
    OPTIONAL COMPOUND(LITERAL :, $returnType = ALIAS ValueType)
    $body = OPTIONAL OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

RULE ConstructorDecl
    LITERAL constructor
    MOVE
    $body = OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

RULE DestructorDecl
    LITERAL destructor
    MOVE
    $body = OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

RULE GetAccessorDecl
    $modifiers = OPTIONAL REPEATED RULE Modifier
    KEYWORD get
    MOVE
    $name = RULE Identifier
    LITERAL :
    $type = ALIAS ValueType
    $body = OPTIONAL OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

RULE SetAccessorDecl
    $modifiers = OPTIONAL REPEATED RULE Modifier
    KEYWORD set
    MOVE
    $name = RULE Identifier
    LITERAL :
    $type = ALIAS ValueType
    $body = OPTIONAL OR(RULE CodeBlock, RULE ArrowBlock)
END RULE

ALIAS AccessorDecl OR(RULE GetAccessorDecl, RULE SetAccessorDecl)

RULE FuncCallExpr
    $callee = OR(RULE GroupingExpr, RULE Identifier)
    $generics = OPTIONAL RULE GenericArgs
    LITERAL (
    MOVE
    OPTIONAL REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($params = RULE Expr, ALIAS NewlineOrComma)
    $params = OPTIONAL RULE Expr
    OPTIONAL ALIAS NewlineOrComma
    LITERAL )
END RULE

RULE GroupingExpr
    LITERAL (
    $expr = RULE Expr
    LITERAL )
END RULE

RULE MatchTestCatchAll
    LITERAL _
END RULE

RULE MatchTestEnumWithData
    $variant = RULE Identifier
    $data = RULE AnonymousType
END RULE

RULE MatchArm
    $test = OR(RULE MatchTestCatchAll, RULE MatchTestEnumWithData, RULE Expr)
    MOVE
    LITERAL =>
    $statement = ALIAS BlockStatement
END RULE

RULE MatchExpr
    KEYWORD match
    MOVE
    $matchee = RULE Expr
    LITERAL {
    REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($arms = RULE MatchArm, REPEATED ALIAS NewlineOrComment)
    LITERAL }
END RULE

RULE NewExpr
    $type = ALIAS ValueType
    $fields = RULE FieldSet
END RULE

RULE NewArrayExpr
    $type = RULE ArrayType
    LITERAL (
    $initialValue = RULE Expr
    LITERAL ,
    $length = RULE Expr
    LITERAL )
END RULE

RULE ConstArrayExpr
    $type = RULE ArrayType
    LITERAL {
    OPTIONAL REPEATED COMPOUND($elements = RULE Expr, ALIAS NewlineOrComma)
    OPTIONAL COMPOUND($elements = RULE Expr, OPTIONAL ALIAS NewlineOrComma)
    LITERAL }
END RULE

RULE DerefExpr
    KEYWORD deref
    $pointer = RULE Expr
END RULE

ALIAS ExprOperand OR(RULE FuncCallExpr, RULE GroupingExpr, RULE MatchExpr, RULE NewArrayExpr, RULE ConstArrayExpr, RULE NewExpr, RULE Null, RULE Bool, RULE Integer, RULE Char, RULE String, RULE DerefExpr, RULE ExplicitQualifiedPath, RULE Identifier)

RULE LetStatementKind
    OR(KEYWORD let, KEYWORD const)
END RULE

RULE LetStatement
    $modifiers = OPTIONAL REPEATED RULE Modifier
    $kind = RULE LetStatementKind
    MOVE
    $name = RULE Identifier
    OPTIONAL COMPOUND(LITERAL :, $type = ALIAS ValueType)
    OPTIONAL COMPOUND(LITERAL =, $initialValue = RULE Expr)
END RULE

RULE ReturnStatement
    KEYWORD return
    MOVE
    $operand = OPTIONAL RULE Expr
END RULE

RULE IfStatement
    KEYWORD if
    MOVE
    $tests = RULE Expr
    $conditionalBlocks = RULE CodeBlock
    OPTIONAL REPEATED COMPOUND(KEYWORD else, KEYWORD if, $tests = RULE Expr, $conditionalBlocks = RULE CodeBlock)
    OPTIONAL COMPOUND(KEYWORD else, $fallback = RULE CodeBlock)
END RULE

RULE ForLoop
    KEYWORD for
    $condition = RULE Expr
    $block = RULE CodeBlock
END RULE

RULE ForEachLoop
    KEYWORD for
    $elementName = RULE Identifier
    KEYWORD in
    MOVE
    $iterable = RULE Expr
    $block = RULE CodeBlock
END RULE

RULE IndefiniteLoop
    KEYWORD for
    $block = RULE CodeBlock
END RULE

RULE LiteralDeref
    KEYWORD deref
END RULE

RULE OpAssignSelf
    LITERAL =
END RULE

RULE OpAddSelf
    LITERAL +=
END RULE

RULE OpSubtractSelf
    LITERAL -=
END RULE

ALIAS SelfOperator OR(RULE OpAssignSelf, RULE OpAddSelf, RULE OpSubtractSelf)

RULE AssignStatement
    $deref = OPTIONAL RULE LiteralDeref
    $lhs = RULE Expr
    $op = ALIAS SelfOperator
    $rhs = RULE Expr
END RULE

RULE ThrowStatement
    KEYWORD throw
    $error = RULE Expr
END RULE

// ALIAS BlockStatement OR(RULE TryStatement, RULE BreakStatement, RULE ContinueStatement, RULE WhileLoop, RULE IndefiniteLoop, RULE UnsafeBlock, RULE FixedBlock, RULE CodeBlock, RULE AssignStatement, RULE ImportStatement, RULE LetStatement, RULE ReturnStatement, RULE UnreachableStatement, RULE ThrowStatement, RULE Expr)
ALIAS BlockStatement OR(RULE LineComment, RULE ThrowStatement, RULE ReturnStatement, RULE LetStatement, RULE IfStatement, RULE ForEachLoop, RULE ForLoop, RULE IndefiniteLoop, RULE UnsafeBlock, RULE AssignStatement, RULE Expr)

RULE LineComment
    LITERAL //
    UNTIL LITERAL NEWLINE
END RULE

ALIAS NewlineOrComment OR(LITERAL NEWLINE, RULE LineComment)

RULE UnsafeBlock
    KEYWORD unsafe
    $inner = RULE CodeBlock
END RULE

RULE CodeBlock
    LITERAL {
    MOVE
    REPEATED ALIAS NewlineOrComment
    OPTIONAL REPEATED COMPOUND($statements = ALIAS BlockStatement, REPEATED ALIAS NewlineOrComment)
    LITERAL }
END RULE

RULE ArrowBlock
    LITERAL =>
    MOVE
    $expr = RULE Expr
END RULE

RULE PrefixOperator
    OR(LITERAL !, LITERAL ...)
END RULE

RULE InfixOperator
    OR(LITERAL ., LITERAL ==, LITERAL !=, LITERAL +, LITERAL -, LITERAL *, LITERAL /, LITERAL %, LITERAL <<, LITERAL >>, LITERAL or, LITERAL and, LITERAL <=, LITERAL >=, LITERAL <, LITERAL >, LITERAL ??)
END RULE

RULE OpNullAssert CONTIGUOUS
    LITERAL !
    NOT LITERAL =
END RULE

RULE OpIndex
    LITERAL [
    MOVE
    $index = RULE Expr
    LITERAL ]
END RULE

RULE OpAs
    KEYWORD as
    MOVE
    $type = ALIAS ValueType
END RULE

RULE PostfixOperator
    OR(RULE OpNullAssert, RULE OpIndex, RULE OpAs)
END RULE

RULE Expr
    $parts = OPTIONAL REPEATED RULE PrefixOperator
    $parts = ALIAS ExprOperand
    $parts = OPTIONAL REPEATED RULE PostfixOperator
    OPTIONAL REPEATED COMPOUND($parts = RULE InfixOperator, $parts = OPTIONAL REPEATED RULE PrefixOperator, $parts = ALIAS ExprOperand, $parts = OPTIONAL REPEATED RULE PostfixOperator)
END RULE

ALIAS ConstExpr OR(RULE Null, RULE Bool, RULE Integer, RULE Char, RULE String)

RULE FieldDecl
    $modifiers = OPTIONAL REPEATED RULE Modifier
    $name = RULE Identifier
    LITERAL :
    MOVE
    $type = ALIAS ValueType
    OPTIONAL COMPOUND(LITERAL =, $initialValue = RULE Expr)
END RULE

RULE AssociatedTypeDecl
    KEYWORD type
    $name = RULE Identifier
    OPTIONAL COMPOUND(LITERAL =, $aliasee = ALIAS ValueType)
END RULE

ALIAS ClassBodyStatement OR(RULE AssociatedTypeDecl, RULE ConstructorDecl, RULE DestructorDecl, ALIAS AccessorDecl, RULE MethodDecl, RULE FieldDecl, RULE MacroCall)

RULE ClassDeclType
    OR(KEYWORD class, KEYWORD struct, KEYWORD interface)
END RULE

RULE ClassDeclTypeEnum
    KEYWORD enum
END RULE

RULE ClassExtends
    KEYWORD extends
    MOVE
    $superclass = RULE QualifiedPath
    $generics = OPTIONAL RULE GenericArgs
END RULE

RULE ClassDecl
    $modifiers = OPTIONAL REPEATED RULE Modifier
    $type = RULE ClassDeclType
    MOVE
    $name = RULE Identifier
    $generics = OPTIONAL RULE GenericDecls
    $extends = OPTIONAL RULE ClassExtends
    LITERAL {
    REPEATED ALIAS NewlineOrComment
    OPTIONAL REPEATED COMPOUND($statements = ALIAS ClassBodyStatement, REPEATED ALIAS NewlineOrComment)
    LITERAL }
END RULE

RULE EnumDecl
    $modifiers = OPTIONAL REPEATED RULE Modifier
    $type = RULE ClassDeclTypeEnum
    MOVE
    $name = RULE Identifier
    $generics = OPTIONAL RULE GenericDecls
    LITERAL {
    REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($members = RULE EnumMemberStatement, REPEATED LITERAL NEWLINE)
    OPTIONAL REPEATED COMPOUND($statements = ALIAS ClassBodyStatement, REPEATED LITERAL NEWLINE)
    LITERAL }
END RULE

RULE AnonymousTypeField
    $name = RULE Identifier
    OPTIONAL COMPOUND(LITERAL :, $type = ALIAS ValueType)
END RULE

RULE AnonymousType
    LITERAL {
    MOVE
    OPTIONAL REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($fields = RULE AnonymousTypeField, ALIAS NewlineOrComma)
    OPTIONAL COMPOUND($fields = RULE AnonymousTypeField, OPTIONAL ALIAS NewlineOrComma)
    LITERAL }
END RULE

RULE FieldSetField
    $name = RULE Identifier
    OPTIONAL COMPOUND(LITERAL =, $value = RULE Expr)
END RULE

RULE FieldSet
    LITERAL {
    OPTIONAL REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($fields = RULE FieldSetField, ALIAS NewlineOrComma)
    LITERAL }
END RULE

RULE EnumMemberStatement
    $name = RULE Identifier
    $data = OPTIONAL RULE AnonymousType
END RULE

RULE ImplementDecl
    KEYWORD implement
    MOVE
    $generics = OPTIONAL RULE GenericDecls
    $interface = RULE Identifier
    $interfaceGenerics = OPTIONAL RULE GenericArgs
    KEYWORD for
    $target = RULE Identifier
    $targetGenerics = OPTIONAL RULE GenericArgs
    LITERAL {
    REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($statements = ALIAS ClassBodyStatement, REPEATED LITERAL NEWLINE)
    LITERAL }
END RULE

RULE QualifiedPath
    RULE Identifier
    OPTIONAL REPEATED COMPOUND(LITERAL ::, RULE Identifier)
END RULE

RULE ExplicitQualifiedPath
    RULE Identifier
    LITERAL ::
    RULE Identifier
    OPTIONAL REPEATED COMPOUND(LITERAL ::, RULE Identifier)
END RULE

RULE NamespaceStatement
    KEYWORD namespace
    MOVE
    $namespace = RULE QualifiedPath
END RULE

RULE ImportStatement
    KEYWORD import
    MOVE
    LITERAL (
    REPEATED ALIAS NewlineOrComment
    $modules = RULE QualifiedPath
    OPTIONAL REPEATED COMPOUND(REPEATED LITERAL NEWLINE, $modules = RULE QualifiedPath)
    OPTIONAL REPEATED ALIAS NewlineOrComment
    LITERAL )
END RULE

RULE MacroName CONTIGUOUS
    LITERAL $
    RULE Identifier
END RULE

RULE MacroParamDecl CONTIGUOUS
    LITERAL #
    RULE Identifier
END RULE

RULE MacroBody
    UNTIL KEYWORD end macro
END RULE

RULE MacroDecl
    KEYWORD macro
    MOVE
    $name = RULE MacroName
    LITERAL (
    MOVE
    OPTIONAL REPEATED LITERAL NEWLINE
    OPTIONAL REPEATED COMPOUND($params = RULE MacroParamDecl, LITERAL COMMA, OPTIONAL REPEATED LITERAL NEWLINE)
    $params = OPTIONAL RULE MacroParamDecl
    OPTIONAL COMPOUND (LITERAL COMMA, REPEATED LITERAL NEWLINE)
    LITERAL )
    $body = RULE MacroBody
END RULE

RULE MacroArgValue
    LITERAL `
    UNTIL LITERAL `
END RULE

RULE MacroCall
    $name = RULE MacroName
    MOVE
    LITERAL (
    OPTIONAL REPEATED COMPOUND($params = RULE MacroArgValue, LITERAL COMMA, OPTIONAL REPEATED LITERAL NEWLINE)
    $params = OPTIONAL RULE MacroArgValue
    OPTIONAL COMPOUND (LITERAL COMMA, REPEATED LITERAL NEWLINE)
    LITERAL )
END RULE

ALIAS RootStatement OR(RULE ImportStatement, RULE LetStatement, RULE MacroDecl, RULE ClassDecl, RULE EnumDecl, RULE ImplementDecl, RULE FunctionDecl, RULE MacroCall)

RULE Program
    MOVE
    OPTIONAL REPEATED ALIAS NewlineOrComment
    $namespace = RULE NamespaceStatement
    OPTIONAL REPEATED ALIAS NewlineOrComment
    OPTIONAL REPEATED COMPOUND($statements = ALIAS RootStatement, REPEATED ALIAS NewlineOrComment)
END RULE
