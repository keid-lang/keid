namespace std::fs

import (
    core::io
    std::io
    std::os
)

public class FileSystemObject {
    handle: usize
    get open: bool => this.handle == 0

    destructor {
        this.close()
    }

    assertOpen() {
        if !this.open {
            throw error.io("file handle is closed")
        }
    }

    abstract closeImpl()
}

implement Closeable for FileSystemObject {
    close() {
        // closing more than once is a no-op
        if !this.open {
            return
        }

        this.closeImpl()
        this.handle = 0
    }
}

public class Directory extends FileSystemObject {
    override closeImpl() {
        std::impl::fs.closeDirectory(this.handle)
    }
}

public enum FileOpenMode {
    ReadOnly
    ReadWrite
    ReadWriteTruncate
    WriteOnly
    AppendOnly
    ReadAppend
}
 
public class File extends FileSystemObject {
    public static open(path: string, mode: FileOpenMode): File {
        let modeStr = match mode {
            ReadOnly => "r"
            ReadWrite => "r+"
            ReadWriteTruncate => "w+"
            WriteOnly => "w"
            AppendOnly => "a"
            ReadAppend => "a+"
        }
        return new File {
            handle = std::impl::fs.openFile(path, modeStr)
        }
    }

    override closeImpl() {
        std::impl::fs.closeFile(this.handle)
    }

    public seek(offset: usize) {
        std::impl::fs.seekFile(this.handle, offset)
    }

    /// Reads all the bytes in the file from start to end, regardless of the current cursor position.
    public readAllBytes(): [uint8] {
        let pos = std::impl::fs.getSeekFilePos(this.handle)
        this.seek(0)

        let buf = new uint8[0; this.length]
        this.read(buf)

        this.seek(pos)

        return buf
    }

    /// Writes all the bytes to the file, beginning at the start of the file.
    /// This function does not affect the cursor position.
    public writeAllBytes(bytes: [uint8]) {
        let pos = std::impl::fs.getSeekFilePos(this.handle)
        this.seek(0)
        this.write(bytes)
        this.seek(pos)
    }

    public get length: usize {
        this.assertOpen()

        return std::impl::fs.getFileLength(this.handle)
    }
}

implement Reader for File {
    read(buf: [uint8]): isize {
        this.assertOpen()

        return std::impl::fs.readFileBytes(this.handle, buf)
    }
}

implement Writer for File {
    write(buf: [uint8]): isize {
        this.assertOpen()

        return std::impl::fs.writeFileBytes(this.handle, buf)
    }
}
